WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{
    ("/*" ~ (!"*/" ~ ANY)* ~ "*/" ) |
    ("//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE) |
    ("#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE)
}

Plus = { "+" }
Minus = { "-" }
Star = { "*" }
Slash = { "/" }
Modulus = { "%" }

GT = { ">" }
GE = { ">=" }
LT = { "<" }
LE = { "<=" }
EQ = { "==" }
NE = { "!=" }

LAnd = { "&&" }
LOr = { "||" }
Not = { "!" }
Neg = { "-" }

Inc = { "++" }
Dec = { "--" }

Assign = { "=" }
Deref = { "->" }

LPar = { "(" }
RPar = { ")" }
RCurl = { "}" }
LCurl = { "{" }
Semi = { ";" }

Null = { "null" }

//AddOp = { Plus | Minus }
//MulOp = { Star | Slash | Modulus }
//RelOp = { GT | GE | LT | LE | EQ | NE }

Digit = _{ '0'..'9' }
Alpha = _{ ASCII_ALPHA | "_" }

Boolean = { "true" | "false" }
Integer = @{ Digit+ }
Double = @{ Digit+ ~ "." ~ Digit+ ~ (("e" | "E") ~ ("+" | "-")? ~ Digit+)? }
Char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    //| "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
String = @{ "\"" ~ Char* ~ "\"" }


Primitive = { "int" | "double" | "boolean" | "void" }

NonType = _{ Boolean | Return | If | Else | While | Null }
NonIdent = _{ Primitive | NonType }

Ident = @{
    (NonIdent ~ (Digit | Alpha)+) |
    (!NonIdent ~ Alpha ~ (Digit | Alpha)*)
}

Type = @{
    (NonType ~ (Digit | Alpha)+) |
    (!NonType ~ Alpha ~ (Digit | Alpha)*)
}
FunType = { Type ~ "(" ~ (Type ~ ("," ~  Type)*)? ~ ")"}

Variable = { (Ident ~ Deref ~ Variable) | Ident }

Expr6 = {
    (Ident ~ LPar ~ (Expr ~ ("," ~ Expr)*)? ~ RPar ) |
    (LPar ~ Type ~ RPar ~ Null) |
    (LPar ~ Expr ~ RPar) |
    (New ~ Type) |
    Double |
    Integer |
    Boolean |
    Variable |
    String
}

Expr5 = { (Not | Neg)? ~ Expr6 }
Expr4 = { Expr5 ~ ((Star | Slash | Modulus) ~ Expr5)* }
Expr3 = { Expr4 ~ ((Plus | Minus) ~ Expr4)* }
Expr2 = { Expr3 ~ ((GE | GT | LE | LT | EQ | NE) ~ Expr3)* }
Expr1 = { Expr2 ~ (LAnd ~ Expr2)* }
Expr = { Expr1 ~ (LOr ~ Expr1)* }

Return = { "return" }
If = { "if" }
Else = { "else" }
While = { "while" }
New = { "new" }

DeclItem = { Ident ~ Assign ~ Expr | Ident }

Stmt = {
    ( Type ~ (DeclItem ~ ("," ~ DeclItem)*)? ~ ";") |
    ( Return ~ Expr? ~ ";") |
    ( If ~ "(" ~ Expr ~ ")" ~ Stmt ~ Else ~ Stmt) |
    ( If ~ "(" ~ Expr ~ ")" ~ Stmt) |
    ( While ~ "(" ~ Expr ~ ")" ~ Stmt) |
    Blk |
    (Variable ~ Assign ~ Expr  ~ ";") |
    (Variable ~ Inc ~ ";") |
    (Variable ~ Dec ~ ";") |
    (Expr ~ ";") |
    ";"
}

Blk = { "{" ~ Stmt* ~ "}" }

Arg = { Type ~ Ident }

StructDefPrefix = @{ "struct" ~ WHITESPACE }
StructDef = { StructDefPrefix ~ Ident ~ "{" ~ (Arg ~ ";")* ~ "}" ~ ";" }

TypeDefPrefix = @{ "typedef" ~ WHITESPACE+ ~ StructDefPrefix}
TypeDef = { TypeDefPrefix ~ Ident ~ "*" ~ Ident ~ ";" }

Function = { Type ~ Ident ~ "(" ~ (Arg ~ ("," ~ Arg)*)? ~ ")" ~ Blk }

TopDef = _{ Function | StructDef | TypeDef }
Program = { SOI ~ TopDef+ ~ EOI}

